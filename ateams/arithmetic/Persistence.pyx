
# distutils: language=c++

from .common cimport INDEXFLAT, FFINT, TABLECONTIG, FLATCONTIG
from .common import FINT

import numpy as np
cimport numpy as np

from cython.operator cimport dereference
from libcpp.vector cimport vector as Vector
from libcpp.unordered_set cimport unordered_set as Set
from libcpp.set cimport set as OrderedSet
from libcpp.unordered_map cimport unordered_map as Map
from libcpp cimport bool
from libc.math cimport pow


cdef class Persistence:
	"""
	Computes the persistent homology of a complex with coefficients
	in an arbitrary finite field.
	"""
	def __init__(
			self,
			FFINT characteristic,
			list[list[int]] flattened,
			int homology=-1
		):
		"""
		Args:
			characteristic (int): Characteristic of the finite field over which
				we do computations.
			flattened (list): List-of-lists representing the *original* flattened
				boundary matrix for the complex.
			homology (int=-1): Degree of homology observed for homological percolation events;
				if not specified, the homology of the codimension-1 skeleton is
				reported.

		The `Persistence.ComputePercolationEvents` method computes the birth
		times of giant cycles given a complete filtration — that is, a filtration
		where the terminal element is the entire cubical complex. For example,
		the code below finds the birth times of giant cycles on the default
		\(3 \\times 3\) cubical torus: the filtration stored in `filtration`
		adds each cube to the cubical complex in order of construction. At
		completion, `events` contains the times `{17, 21}` which correspond to
		crossings of the meridian and equator of the torus, respectively.
		
		
			from ateams.arithmetic import Persistence
			from ateams.structures import Lattice
			
			L = Lattice().fromCorners([3,3], field=3)
			P = Persistence(L.field.characteristic, L.flattened, homology=1)

			filtration = np.arange(len(L.flattened))
			events = P.ComputePercolationEvents(filtration)


		The `Persistence.ComputeBettiNumbers` method provides functionality to compute the
		Betti numbers for an entire subcomplex. The subcomplex is specified by
		a list of indices corresponding to the cells included in the subcomplex.
		Initializing the `Persistence` object as before, we can get the Betti
		numbers of the \(2\)-torus encoded by the `Lattice` above by passing a
		list of all cells' indices in the flattened boundary matrix: since there
		are \(36\) cells (including vertices, edges, and squares), the `subcomplex`
		is just the range of integers \(0-36\):


			from ateams.arithmetic import Persistence
			from ateams.structures import Lattice
			
			L = Lattice().fromCorners([3,3], field=3)
			P = Persistence(L.field.characteristic, L.flattened)

			subcomplex = np.arange(len(L.flattened))

			bettis = P.ComputeBettiNumbers(subcomplex)


		The result in `bettis` is the list `[1,2,1]`. By deleting an edge, we
		reduce the number of squares by two, so we should expect the homology
		of a one-edge-deleted \(2\)-torus to have betti numbers `[1,2,0]`. To
		access the indices of the flattened boundary matrix corresponding to
		cells of each dimension, we can use the `Lattice.tranches` property
		which maps an integer dimension to a (start, stop) pair: for example,
		`L.tranches[1]` is `[9, 27]` in the lattice above, so we know that all
		entries in the flattened boundary matrix between indices \(9\) and \(27\)
		(_right-exclusive_) correspond to edges. To test our theory, we
		construct a subcomplex by deleting the first edge added:


			from ateams.arithmetic import Persistence
			from ateams.structures import Lattice
			
			L = Lattice().fromCorners([3,3], field=3)
			P = Persistence(L.field.characteristic, L.flattened)

			subcomplex = np.arange(len(L.flattened))
			firstEdge = L.tranches[1][0]
			subcomplex = np.concatenate([subcomplex[:firstEdge], subcomplex[firstEdge+1:]])

			bettis = P.ComputeBettiNumbers(subcomplex)


		The result in `bettis` is the list `[1,2,0]`, as expected. Note that we
		only need to pass a `homology` parameter to the `Persistence` constructor
		if we call `Persistence.ComputePercolationEvents`.
		"""
		self.characteristic = characteristic;
		self.boundary = self.Vectorize(flattened);
		
		# If there was no homology parameter passed, just compute the homology
		# of the codimension-1 skeleton.
		if homology < 0:
			# Knock off *two* dimensions (so we don't get off-by-one errors), and
			# make sure we're computing at least the zeroth homology.
			self.homology = self._tranches.size()-2;
			if self.homology < 0: self.homology = 0;
		else: self.homology = homology;

		self.cellCount = self._boundary.size();
		self.vertexCount = self._tranches[0][1];
		self.higherCellCount = self._tranches[self.homology+1][1];
		self.low = self._tranches[self.homology][0];
		self.high = self._tranches[self.homology][1];

		# Construct arithmetic operations.
		self.__arithmetic();

		# Load premarked indices into a `Vector` so we can refresh quickly.
		cdef int i;

		self.premarked = Vector[int](self.vertexCount);
		for i in range(self.vertexCount): self.premarked[i] = i;

		# Data structures for storing column information.
		self.columnEntries = Vector[OrderedSet[int]](self.cellCount);
		self.columnEntriesIterable = Vector[Vector[int]](self.cellCount);
		self.columnEntriesCoefficients = Vector[Map[int,FFINT]](self.cellCount);

		self.__flushDataStructures();


	cdef void __arithmetic(self) noexcept:
		# Given a field characteristic, construct addition and multiplication
		# tables.
		cdef int p = self.characteristic;
		cdef TABLECONTIG addition, multiplication;
		cdef FLATCONTIG negation, inverse;
		cdef int i, j;

		addition = np.zeros((p,p), dtype=FINT);
		multiplication = np.zeros((p,p), dtype=FINT);

		# Addition, multiplication tables.
		for i in range(p):
			for j in range(p):
				addition[i,j] = <FFINT>((i+j)%p);
				multiplication[i,j] = <FFINT>((i*j)%p);

		self.addition = addition;
		self.multiplication = multiplication;

		# Negations and inverses.
		negation = np.zeros(p, dtype=FINT);
		inverse = np.zeros(p, dtype=FINT);

		negation[0] = 0;
		inverse[0] = 0;

		for i in range(1, p):
			negation[i] = <FFINT>(p-i);
			inverse[i] = <FFINT>(pow(i, p-2)%p);

		self.negation = negation;
		self.inverse = inverse;

	
	cdef void __flushDataStructures(self, bool premark=True) noexcept:
		# Data structures for storing column information.
		self.columnEntries = Vector[OrderedSet[int]](self.cellCount);
		self.columnEntriesIterable = Vector[Vector[int]](self.cellCount);
		self.columnEntriesCoefficients = Vector[Map[int,FFINT]](self.cellCount);

		self.marked = Set[int]();
		if premark: self.marked.insert(self.premarked.begin(), self.premarked.end());
		self.markedIterable = Vector[int](self.cellCount);


	cdef Vector[Vector[int]] ReorderBoundary(self, INDEXFLAT filtration) noexcept:
		"""
		Re-indexes the boundary matrix according to the given filtration.

		Args:
			filtration (np.array): `NumPy` array specifying the order in which
				cells from the complex are added.

		Returns:
			A C++ `std::vector` (cast as a `NumPy` array) containing the reindexed
			boundary matrix.
		"""
		cdef int t, i, j, filtered, unfiltered, face, N, M, dimension, start, stop, please;
		cdef Vector[int] faces, indices;
		cdef Vector[Vector[int]] reordered;

		N = self._boundary.size();
		indices = Vector[int](N);

		# Maps the elements of the filtration to their new indices.
		for t in range(N):
			filtered = filtration[t];
			indices[filtered] = t;

		# Re-orders the (flattened) boundary matrix.
		reordered = Vector[Vector[int]](N);
		for t in range(N):
			reordered[t] = self._boundary[filtration[t]]

		# Re-indexes the re-ordered boundary matrix.
		for t in range(N):
			M = reordered[t].size();

			for i in range(M):
				reordered[t][i] = indices[reordered[t][i]];

		self.boundary = reordered;
		return self.boundary;
		


	cpdef Vector[Vector[int]] ReindexBoundary(self, INDEXFLAT filtration) noexcept:
		"""
		Re-indexes the boundary matrix according to the given filtration.

		Args:
			filtration (np.array): `NumPy` array specifying the order in which
				cells from the complex are added.

		Returns:
			A C++ `std::vector` (cast as a `NumPy` array) containing the reindexed
			boundary matrix.
		"""
		cdef int t, i, j, filtered, unfiltered, face, N, M, dimension, start, stop, please;
		cdef Vector[int] faces, indices, temp, high;

		N = self._boundary.size();
		indices = Vector[int](N);

		# Maps the elements of the filtration to their new indices.
		for t in range(N):
			filtered = filtration[t];
			indices[filtered] = t;

		start = self._tranches[self.homology][0];
		stop = self._tranches[self.homology][1];
		please = self._tranches[self.homology+1][1];

		# Swaps elements when necessary, relabels elements when necessary.
		for i in range(start, please+1):
			# We're swapping elements of dimension `self.homology`:
			if i >= start and i < stop:
				filtered = filtration[i];
				self.boundary[i] = self._boundary[filtered];
			# Otherwise, we're relabeling indices.
			elif i >= stop and i < please:
				faces = self._boundary[i];
				M = faces.size();

				for j in range(M):
					unfiltered = faces[j];
					filtered = indices[unfiltered];
					self.boundary[i][j] = filtered;
		
		return self.boundary


	cdef Vector[Vector[int]] ReindexSubBoundary(self, INDEXFLAT subcomplex) noexcept:
		# Create a "subboundary" matrix that maps old boundary indices to new
		# ones.
		cdef Set[int] included;
		cdef Vector[int] renumbering, faces, retained, dimensions;
		cdef Vector[Vector[int]] subboundary, subsubboundary;
		cdef bool retain;
		cdef int i, j, M, N, dimension;

		# Keep track of which indices are included in the subcomplex.
		N = subcomplex.shape[0];
		included = Set[int]();
		for i in range(N): included.insert(subcomplex[i]);

		# Build the subcomplex.
		M = self._boundary.size();
		renumbering = Vector[int](M);
		subboundary = Vector[Vector[int]](M);
		retained = Vector[int]();

		for i in range(M):
			# If this cell is *not* included in the subcomplex, throw it out
			# and continue.
			retain = included.contains(i);

			# Check whether all the faces belong.
			faces = self._boundary[i];
			N = faces.size();

			for j in range(N):
				# Check whether this face is included in the subcomplex. If it
				# is *not* included, we do *not* include the cell, which means
				# we have to remove it from the set of included cell indices.
				retain = retain and included.contains(faces[j]);

				if not retain:
					included.erase(i);
					break;

			# If this cell should be included, note its original index and new
			# index.
			if retain:
				retained.push_back(i);
				renumbering[i] = retained.size()-1;
		
		# Now, construct the subboundary matrix.
		M = retained.size();
		subboundary = Vector[Vector[int]](M);
		dimensions = Vector[int](M);

		for i in range(M):
			subboundary[i] = self._boundary[retained[i]];
			N = subboundary[i].size();
			dimensions[i] = <int>(N/2);

			for j in range(N):
				subboundary[i][j] = renumbering[subboundary[i][j]];

		self._dimensions = dimensions;
		self.boundary = subboundary;

		# Scan over the array of dimensions, checking how many cells of each
		# dimension exist (and where their indices stop/start).
		cdef Vector[Vector[int]] tranches = Vector[Vector[int]]();
		cdef Vector[int] tranche;
		cdef int t, _tranche = 0;

		N = dimensions.size();

		for t in range(1, N):
			if dimensions[t] > dimensions[t-1] or t == N-1:
				tranche = Vector[int](2);
				tranche[0] = _tranche;
				tranche[1] = t if t < N-1 else N;
				tranches.push_back(tranche);

				_tranche = t;

		self._tranches = tranches;

		# Re-count cells.
		self.cellCount = self.boundary.size();
		self.vertexCount = self._tranches[0][1];
		self.higherCellCount = self._tranches[self.homology+1][1];
		self.low = self._tranches[self.homology][0];
		self.high = self._tranches[self.homology][1];

		# Set pre-marked vertices again.
		self.premarked = Vector[int](self.vertexCount);
		for i in range(self.vertexCount): self.premarked[i] = i;

		return self.boundary
		

	cdef Vector[Vector[int]] Vectorize(self, list[list[int]] flattened) noexcept:
		"""
		Convert a list of lists into C++ vectors.

		Args:
			flattened (list): Sparse boundary matrix.

		Returns:
			C++ `std::vector` (cast as as `NumPy` array) representing the same data.
		"""
		cdef Vector[Vector[int]] outer;
		cdef Vector[int] inner, dimensions;
		cdef int i, j, lower, M, N, dimension;

		# We only want to get boundary matrices for indices of dimension 1 or
		# greater; i.e. we're excluding vertices.
		M = len(flattened);
		outer = Vector[Vector[int]](M);
		dimensions = Vector[int](M);

		for i in range(M):
			# If we're dealing with a non-vertex, we just copy the original
			# boundary matrix over; otherwise, we're dealing with a vertex, which
			# has no boundary.
			try:
				N = len(flattened[i]);
				inner = Vector[int](N);
				for j in range(N): inner[j] = flattened[i][j];

				dimension = <int>(N/2);
			except:
				inner = Vector[int](0);
				dimension = 0;
			
			dimensions[i] = dimension;
			outer[i] = inner;

		# Set this as the current boundary matrix, but also return it (for
		# completion's sake).
		self._boundary = outer;
		self._dimensions = dimensions;

		# Scan over the array of dimensions, checking how many cells of each
		# dimension exist (and where their indices stop/start).
		cdef Vector[Vector[int]] tranches = Vector[Vector[int]]();
		cdef Vector[int] tranche;
		cdef int t, _tranche = 0;

		N = dimensions.size();

		for t in range(1, N):
			if dimensions[t] > dimensions[t-1] or t == N-1:
				tranche = Vector[int](2);
				tranche[0] = _tranche;
				tranche[1] = t if t < N-1 else N;
				tranches.push_back(tranche);

				_tranche = t;

		self._tranches = tranches;
		
		return outer;


	cdef OrderedSet[int] Eliminate(self, int youngest, OrderedSet[int] faces, Map[int,FFINT] &faceCoefficients) noexcept:
		"""
		Performs Gaussian elimination on the row specified by `faceCoefficients`
		and the row with a pivot in column `youngest`.

		.. WARNING: Cannot be called from Python; internal only.

		Args:
			youngest (int): Pivot column.
			faces (std::set): Ordered set of faces.
			faceCoefficients (&std::unordered_map[int,FFINT]): Unordered map taking
				indices to coefficients; this represents a row in the matrix.

		Returns:
			`std::set` of remaining faces.
		"""
		cdef Vector[int] entriesIterable;
		cdef int i, entry, N;
		cdef FFINT _q, inverse, q, entryCoefficient, faceCoefficient, mul, add;

		# Get the coefficient of the pivot entry, then eliminate the row.
		_q = self.columnEntriesCoefficients[youngest][youngest];
		inverse = self.inverse[_q];

		entriesIterable = self.columnEntriesIterable[youngest];
		N = entriesIterable.size();

		# Eliminate.
		for i in range(N):
			# `entry` is the index of the row (column?) we're trying to eliminate.
			entry = entriesIterable[i];
			entryCoefficient = self.columnEntriesCoefficients[youngest][entry];
			mul = self.multiplication[inverse,entryCoefficient];
			q = self.negation[mul];

			# If `add` is 0, we don't care about tracking it anymore; throw it
			# out of the faces (and remove it from the dict, if it were in there
			# in the first place?).
			if faces.contains(entry):
				faceCoefficient = faceCoefficients[entry];
				add = self.addition[faceCoefficient,q];
			else:
				add = q;

			if add < 1:
				faces.erase(entry);
				faceCoefficients.erase(entry);
			else:
				faces.insert(entry);
				faceCoefficients[entry] = add;
			
		return faces


	cdef OrderedSet[int] RemoveUnmarkedCells(self, int cell, OrderedSet[int] faces, Map[int,FFINT] &faceCoefficients) noexcept:
		"""
		Given the latest cell added to the filtration, remove unmarked
		(nonpivot) entries from its boundary.

		Args:
			int (cell): The most recent cell added to the filtration.

		Returns:
			An `OrderedSet` of `int`s corresponding to (indices of!) pivot
			entries in the boundary of cell `cell`.
		"""
		cdef int i, face, parity, N;
		N = self.boundary[cell].size();

		for i in range(N):
			face = self.boundary[cell][i];

			# If the face is unmarked, throw it out.
			if self.marked.contains(face):
				faces.insert(face);

				if (i%2) < 1: faceCoefficients[face] = self.negation[1];
				else: faceCoefficients[face] = self.negation[self.characteristic-1];
		
		return faces;

	
	cdef OrderedSet[int] ReducePivotRow(self, int cell, OrderedSet[int] faces, Map[int,FFINT] &faceCoefficients) noexcept:
		"""
		Reduces the pivot row corresponding to cell `cell`.

		Args:
			cell (int): The most recent cell added to the filtration.

		Returns:
			An ordered set of indices corresponding to nonzero entries in the
			of the pivot column.
		"""
		cdef OrderedSet[int] youngestColumnEntries;
		cdef int youngest;
		cdef FFINT _q, q;

		faces = self.RemoveUnmarkedCells(cell, faces, faceCoefficients);

		while not faces.empty():
			# Get the face of `cell` of maximum degree (i.e. the one added latest
			# to the complex).
			youngest = dereference(faces.rbegin());
			youngestColumnEntries = self.columnEntries[youngest];

			# If the column's empty, we've found a pivot, and we're done.
			if youngestColumnEntries.empty(): break;

			# Otherwise, eliminate.
			faces = self.Eliminate(youngest, faces, faceCoefficients);
		
		return faces;
			

	cpdef OrderedSet[int] ComputePercolationEvents(self, INDEXFLAT filtration) noexcept:
		"""
		Computes the times of homological percolation events given a filtration
		and a boundary matrix.

		Args:
			filtration (np.array): Order in which cells are added.

		Returns:
			A `set` of times at which homological percolation occurs.
		"""
		# Flush the set of marked indices, adding premarked ones.
		self.__flushDataStructures();
		cdef OrderedSet[int] events = OrderedSet[int]();

		# Construct the boundary matrix for this filtration; variables for
		# objects.
		cdef Vector[Vector[int]] boundary = self.ReindexBoundary(filtration);
		cdef Vector[int] facesIterable, degree = Vector[int](self.cellCount);
		cdef OrderedSet[int] faces;
		cdef Map[int,FFINT] faceCoefficients;
		cdef int t, j, cell, dimension, time, tagged, youngest;

		# TODO: shouldn't have to iterate over vertices
		tagged = 0;

		for t in range(self.vertexCount, self.higherCellCount):
			# Since our filtrations are discrete (i.e. we add exactly one simplex
			# at each time-step), the "degree" of the cell is the same as the time
			# at which it was added.
			cell = t;

			# Create buckets for indices and coefficients; these are created and
			# stored ONCE, but accessed many times.
			faces = OrderedSet[int]();
			faceCoefficients = Map[int,FFINT]();

			# Eliminate.
			faces = self.ReducePivotRow(cell, faces, faceCoefficients);
			
			# If we end up with no faces, we've found a pivot row.
			if faces.empty():
				# Mark the face, but only add it to the marked iterable if it's
				# of the right dimension.
				self.marked.insert(cell);
				degree[cell] = t;
				
				if self._dimensions[cell] == self.homology:
					self.markedIterable[tagged] = cell;
					tagged = tagged + 1;

			# Otherwise, store the row in the appropriate locations.
			else:
				youngest = dereference(faces.rbegin());
				facesIterable = Vector[int]();
				facesIterable.insert(facesIterable.begin(), faces.begin(), faces.end());

				self.columnEntries[youngest] = faces;
				self.columnEntriesIterable[youngest] = facesIterable;
				self.columnEntriesCoefficients[youngest] = faceCoefficients;
				degree[youngest] = t;

		# # Once we're done eliminating, check over what we find.
		cdef OrderedSet[int] unmarked;

		for j in range(tagged):
			cell = self.markedIterable[j];

			unmarked = self.columnEntries[cell];
			if unmarked.empty(): events.insert(degree[cell]);
		
		return events

	
	cpdef OrderedSet[int] ComputeGiantCycles(self, INDEXFLAT filtration) noexcept:
		"""
		Computes the times of homological percolation events given a filtration
		and a boundary matrix.

		Args:
			filtration (np.array): Order in which cells are added.

		Returns:
			A `set` of times at which homological percolation occurs.
		"""
		# Flush the set of marked indices, adding premarked ones.
		self.__flushDataStructures();
		cdef OrderedSet[int] events = OrderedSet[int]();

		# Construct the boundary matrix for this filtration; variables for
		# objects.
		cdef Vector[Vector[int]] boundary = self.ReorderBoundary(filtration);
		print(boundary)
		cdef Vector[int] facesIterable, degree = Vector[int](self.cellCount);
		cdef OrderedSet[int] faces;
		cdef Map[int,FFINT] faceCoefficients;
		cdef int t, j, cell, dimension, time, tagged, youngest;

		# TODO: shouldn't have to iterate over vertices
		tagged = 0;

		for t in range(0, self.cellCount):
			# Since our filtrations are discrete (i.e. we add exactly one simplex
			# at each time-step), the "degree" of the cell is the same as the time
			# at which it was added.
			cell = t;

			# Create buckets for indices and coefficients; these are created and
			# stored ONCE, but accessed many times.
			faces = OrderedSet[int]();
			faceCoefficients = Map[int,FFINT]();

			# Eliminate.
			faces = self.ReducePivotRow(cell, faces, faceCoefficients);
			
			# If we end up with no faces, we've found a pivot row.
			if faces.empty():
				# Mark the face, but only add it to the marked iterable if it's
				# of the right dimension.
				self.marked.insert(cell);
				degree[cell] = t;
				
				if self._dimensions[cell] == self.homology:
					self.markedIterable[tagged] = cell;
					tagged = tagged + 1;

			# Otherwise, store the row in the appropriate locations.
			else:
				youngest = dereference(faces.rbegin());
				facesIterable = Vector[int]();
				facesIterable.insert(facesIterable.begin(), faces.begin(), faces.end());

				self.columnEntries[youngest] = faces;
				self.columnEntriesIterable[youngest] = facesIterable;
				self.columnEntriesCoefficients[youngest] = faceCoefficients;
				degree[youngest] = t;

		# # Once we're done eliminating, check over what we find.
		cdef OrderedSet[int] unmarked;

		for j in range(tagged):
			cell = self.markedIterable[j];

			unmarked = self.columnEntries[cell];
			if unmarked.empty(): events.insert(degree[cell]);
		
		return events
	
	
	cpdef Vector[int] ComputeBettiNumbers(self, INDEXFLAT subcomplex) noexcept:
		"""
		Computes the _Betti numbers_ — the ranks of the homology groups — of the
		subcomplex specified.

		Args:
			subcomplex (np.ndarray): Indices of cells of the _full_ (flattened)
				boundary matrix to include in the complex.

		Returns:
			A C++ `Vector[int]` where the \(i\)th entry is the \(i\)th Betti number.
		"""
		# Construct the boundary matrix for this filtration; variables for
		# objects. Flush the set of marked indices, adding premarked ones.
		cdef Vector[Vector[int]] subboundary = self.ReindexSubBoundary(subcomplex);
		self.__flushDataStructures(premark=False);

		cdef Vector[int] facesIterable, degree = Vector[int](self.cellCount);
		cdef OrderedSet[int] faces;
		cdef Map[int,FFINT] faceCoefficients;
		cdef int t, j, cell, time, tagged, youngest;
		
		tagged = 0;

		for t in range(0, self.cellCount):
			# Since our filtrations are discrete (i.e. we add exactly one simplex
			# at each time-step), the "degree" of the cell is the same as the time
			# at which it was added.
			cell = t;

			# Create buckets for indices and coefficients; these are created and
			# stored ONCE, but accessed many times.
			faces = OrderedSet[int]();
			faceCoefficients = Map[int,FFINT]();

			# Eliminate.
			faces = self.ReducePivotRow(cell, faces, faceCoefficients);
			
			# If we end up with no faces, we've found a pivot row.
			if faces.empty():
				# Mark the face, but only add it to the marked iterable if it's
				# of the right dimension.
				self.marked.insert(cell);
				degree[cell] = t;
				
				self.markedIterable[tagged] = cell;
				tagged = tagged + 1;

			# Otherwise, store the row in the appropriate locations.
			else:
				youngest = dereference(faces.rbegin());
				facesIterable = Vector[int]();
				facesIterable.insert(facesIterable.begin(), faces.begin(), faces.end());

				self.columnEntries[youngest] = faces;
				self.columnEntriesIterable[youngest] = facesIterable;
				self.columnEntriesCoefficients[youngest] = faceCoefficients;
				degree[youngest] = t;

		# Once we're done eliminating, check over what we find.
		cdef OrderedSet[int] unmarked;
		cdef Vector[int] bettis = Vector[int](self._tranches.size(),0);

		for j in range(tagged):
			cell = self.markedIterable[j];
			unmarked = self.columnEntries[cell];
			dimension = self._dimensions[cell];

			if unmarked.empty():
				bettis[dimension] = bettis[dimension] + 1
		
		return bettis

